// Tree with icons
{
    UI::OpenDialog(
               `VBox(
                     `Heading( "YaST2 Mini Control Center" ),
                     `Tree(`id(`mod), `opt(`notify, `multiSelection, `immediate ),
			   "Modules",
			   [
			    `item(`id( "country" ), `icon( "yast-yast-language.png" ), "Localization",  false,
				  [
				   `item(`id( "keyboard"   ), `icon( "yast-keyboard.png"   ), "Keyboard"   ),
				   `item(`id( "timezone"   ), `icon( "yast-timezone.png"   ), "Time zone"  )
				   ]
				  ),
			    `item(`id( "mouse"      ), `icon( "yast-mouse.png"      ), "Mouse"      ),
			    `item(`id( "lan"        ), `icon( "yast-lan.png"        ), "Network"    ),
			    `item(`id( "xmas" ), `icon( "yast-software.png"   ), "Merry X-Mas"   ),
			    `item(`id( "newyear"  ), `icon( "yast-software.png"   ), "Happy New Year"   )
			    ] ),
                     `HBox(`PushButton(`id(`ok), `opt(`default), "&OK"),
                           `PushButton(`id(`deselect), "&Log selected" )
                          )
                     )
               );

    UI::ChangeWidget(`mod, `SelectedItems, ["keyboard", "xmas", "newyear" ] );
    any id = nil;
    string current = (string) UI::QueryWidget( `id(`mod), `CurrentItem );
    y2milestone( "Current item: %1", current );
    repeat
    {
      id = UI::UserInput();
      list selected_items = (list) UI::QueryWidget( `id(`mod), `SelectedItems );
      y2milestone( "Selected items: %1", selected_items );
      current = (string) UI::QueryWidget( `id(`mod), `CurrentItem );
      y2milestone( "Current item: %1", current );
    } until (id == `ok);
}
